material {
    name : blitHigh,
    parameters : [
        {
            type : sampler2d,
            name : color,
            precision: medium
        },
        {
            type : float4,
            name : resolution,
            precision: high
        }
    ],
    variables : [
        vertex
    ],
    depthWrite : false,
    depthCulling : false,
    domain: postprocess
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
    }
}

fragment {
    void dummy() {}

    void postProcess(inout PostProcessInputs postProcess) {
        highp vec2 uv = variable_vertex.xy; // interpolated to pixel center
        vec4 c0, c1;
        c0  = textureLodOffset(materialParams_color, uv, 0.0, ivec2(-1, -1));
        c0 += textureLodOffset(materialParams_color, uv, 0.0, ivec2( 1, -1));
        c0 += textureLodOffset(materialParams_color, uv, 0.0, ivec2( 1,  1));
        c0 += textureLodOffset(materialParams_color, uv, 0.0, ivec2(-1,  1));
        c0 += 4.0 * textureLod(materialParams_color, uv, 0.0);
        c1  = textureLodOffset(materialParams_color, uv, 0.0, ivec2(-1,  0));
        c1 += textureLodOffset(materialParams_color, uv, 0.0, ivec2( 0, -1));
        c1 += textureLodOffset(materialParams_color, uv, 0.0, ivec2( 1,  0));
        c1 += textureLodOffset(materialParams_color, uv, 0.0, ivec2( 0,  1));
        postProcess.color = (c0 + 2.0 * c1) * (1.0 / 16.0);
    }
}

